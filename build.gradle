plugins {
    id 'java'
	id 'net.ltgt.errorprone' version '4.3.0' // https://github.com/tbroyer/gradle-errorprone-plugin
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
	toolchain {
		// Usage of a recent javac version for compatibility with NullAway JSpecify mode
		languageVersion = JavaLanguageVersion.of(25)
	}
}

tasks.withType(JavaCompile).configureEach {
	options.errorprone {
		disableAllChecks = true // Other error prone checks are disabled
		option("NullAway:OnlyNullMarked", "true") // Enable nullness checks only in null-marked code
		error("NullAway") // bump checks from warnings (default) to errors
		option("NullAway:JSpecifyMode", "true") // https://github.com/uber/NullAway/wiki/JSpecify-Support
	}
	// Keep a JDK 17 baseline
	options.release = 17
}

dependencies {
    implementation 'org.jspecify:jspecify:1.0.0' // https://jspecify.dev/
	errorprone 'com.google.errorprone:error_prone_core:2.42.0' // https://github.com/google/error-prone
	errorprone 'com.uber.nullaway:nullaway:0.12.12' // https://github.com/uber/NullAway
}
